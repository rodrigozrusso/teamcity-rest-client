buildscript {
    ext.kotlin_version = '1.1.2-2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
    }
}

dependencies {
    compile project(':teamcity-rest-client-api')
    compile project(':teamcity-rest-client-impl')
}

configure(project(':teamcity-rest-client-api')) {
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }
}

configure(project(':teamcity-rest-client-impl')) {
    dependencies {
        compile project(':teamcity-rest-client-api')
        compile "commons-codec:commons-codec:1.10"
        compile 'com.squareup.retrofit:retrofit:1.9.0'

        compile 'org.slf4j:slf4j-api:1.7.12'

        testCompile 'junit:junit:4.12'
        testCompile 'org.slf4j:slf4j-log4j12:1.7.12'
    }
}

allprojects {
    group = 'org.jetbrains.teamcity'
    if (hasProperty("buildNumber")) {
        version = "$buildNumber"
    }
    apply plugin: 'maven-publish'
    task sourceJar(type: Jar) {
        from sourceSets.main.kotlin
    }
    artifacts {
        archives sourceJar
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }
    if (hasProperty("bintrayUser")) {
        bintray {
            user = bintrayUser
            key = bintrayApiKey
            publish = true
            publications = ['mavenJava']
            pkg {
                userOrg = 'jetbrains'
                repo = "teamcity-rest-client"
                name = "teamcity-rest-client"
                version {
                    name = "$buildNumber"
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    version = '3.5'
}
